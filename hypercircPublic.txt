## Maple script
## Main procedure: 
	### testar_H	
	### input: ellipse,hyperbola,x,y,epsilon
	### output: relative position of both conics, according to Theorem 12.
	### Example:
	### testar_H(  x^2 + 4*x + y^2 - 2/5*y + 76/25, 1 + x^2/4 - y^2,x,y,10^(-5))
	###				"1: Separated"

 

with(LinearAlgebra):  

######################
######################
matriz_conica:=proc(conica,x,y) local a0,a1,a2,a3,a4,a5,M ;
description "return the matrix M that defines the conic";
a0:=coeff(conica,x,2); 
a2:=coeff(conica,y,2); 
a5:=subs(x=0,y=0,conica); 
a1:=coeff(coeff(conica,x,1),y,1)/2; 
a3:=coeff(coeff(conica,x,1),y,0)/2; 
a4:=coeff(coeff(conica,y,1),x,0)/2; 
M:=Matrix(3, 3, {(1, 1) = a0, (1, 2) = a1, (1, 3) = a3, (2, 2) = a2, (2, 3) = a4, (3, 3) = a5}, storage = triangular[upper], shape = [symmetric]);
 
return  M;
end:


######################
######################


signo:=proc(a, epsilon)
description "sign of a";
if abs(evalf(a))<=epsilon then return(0);
	else 
	return(sign(evalf(a)));
fi;
end:

######################
######################

vari:=proc(L) local n,i; ##L : signos
description "given a set of signs, compute the number of sign changes";
n:=0;
for i from 1 to nops(L)-1 do
	if L[i+1] <>0 and  L[i+1] <> L[i]  then n:=n+1;fi;
od;
return n;
end:

######################
######################
 

testar_H:=proc(elipse,hiperbola,x,y,epsilon) local elips, a0,a1,a2,a3,a4,a5,b0,b1,b2,b3,b4,b5,M,N,M2,N2,L0,L1,L2,L3,T,F,G1,I1,I2,I3,I4,I5,H0,H2,H5,T1,T2,K3,K4,K5,J1,J2,J3,J4,J5,sF,sJ,sK,sK3,sK4,sK5,sJ1,sJ2,sJ3,sJ4,sJ5,sI1,sL0,sL1,sL2,sL3,sG1;
 description "given an ellipse and a hyperbola, return their relative position following Theorem 12";

a0:=simplify(coeff(elipse,x,2)); 
a2:=simplify(coeff(elipse,y,2));

if signo(a0,epsilon)=-1 and signo(a2,epsilon)=-1 then elips:=-elipse; else elips:=elipse;fi;

a0:=simplify(coeff(elips,x,2)); 
a2:=simplify(coeff(elips,y,2));


if signo(a0,epsilon)=-1 or signo(a2,epsilon)=-1 then WARNING("hypotheses are not verified");fi;
M:=matriz_conica(elips,x,y);  
N:=matriz_conica(hiperbola,x,y);  

M2:=SubMatrix(M,1..2,1..2);
N2:=SubMatrix(N,1..2,1..2);

F:=collect(Determinant(lambda*N+M),lambda,factor): 

L0:=coeff(F,lambda,3);
L1:=coeff(F,lambda,2);
L2:=coeff(F,lambda,1);
L3:=coeff(F,lambda,0);

  
T1:=Determinant(M2);
T2:=Determinant(N2);
sL0:=signo(evalf(L0),epsilon);
sL3:=signo(evalf(L3),epsilon);

if not (signo(T2,epsilon)=-1 and signo(T1,epsilon)=1 and sL3=-1 and sL0=-1 and signo(a0,epsilon)=1 and signo(a2,epsilon)=1 ) then WARNING("hypotheses are not verified");fi;

 
T:=simplify(T1*Trace(MatrixInverse(M2).N2));
sL1:=signo(evalf(L1),epsilon);
sL2:=signo(evalf(L2),epsilon);


I1:=discrim(F,lambda):
G1:=discrim(diff(F,lambda),lambda); 
sI1:=signo(evalf(I1),epsilon);
sG1:=signo(evalf(G1),epsilon);

H5:=L0/T2;
H2:=(T-sqrt(T^2-4*T1*T2))/(2*T1); 
H0:=(T+sqrt(T^2-4*T1*T2))/(2*T1);
 

K5:=expand(L1 - 3*L0/H2);
K4:=expand(H2^2*L2 - 2*H2*L1 + 3*L0);
K3:=expand((-H2^3*L3 + H2^2*L2 - H2*L1 + L0));
sK5:=signo(evalf(K5),epsilon);
sK4:=signo(evalf(K4),epsilon);
sK3:=signo(evalf(K3),epsilon);


if sK3=1 then WARNING("K3 positive");fi; 

J5:=expand(evalf(H0*L1 - 3*L0));J4:=expand(evalf(H0^2*L2 - 2*H0*L1 + 3*L0));J3:=expand(evalf(H0^3*L3 - H0^2*L2 + H0*L1 - L0));
 
J1:=sqrt(L3*H5/(H2*T1)) - H5*(H0*L1 - L0)/(2*H0*L0); 

sJ5:=signo(evalf(J5),epsilon);
sJ4:=signo(evalf(J4),epsilon);
sJ3:=signo(evalf(J3),epsilon);
sJ1:=signo(evalf(J1),epsilon);

 
sF:=[sL0,sL1,sL2,sL3];
sJ:=[sL0,sJ5,sJ4,sJ3];
sK:=[sL0,sK5,sK4,sK3]; 
  

if sK3=0 and sK4=0 then print ("7: two outer tangent points"); 

elif sJ3=0 and sJ4=0 and sJ5=1 then print("10: ellipse inside hyperbola"); 

elif sJ3=0 and sJ4=0 and sJ5=0 then print("3ii: one inner tangent point");

elif sJ3=0 and sJ1=0 and sJ5=1 and sJ4<>0 then print("3ii: one inner tangent point");

elif sJ3=0 and sJ4=0 and sJ5=-1 then print("4: two inner tangent point");

elif sI1=-1 then print("2: Two points of intersection");  
	
elif sI1=1 then
		if vari(sF)=2 and vari(sK)=0 then print("1: Separated");
		elif vari(sJ)=0 then print("9: Four intersections points,1");
		elif vari(sK)>0 then print("9: Four intersections points,2");
		elif vari(sF)=0 and vari(sJ)>0 and sJ3=0 and sJ4<>0 then print("10:ellipse inside hyperbola");
		elif vari(sF)=0 and vari(sJ)=2 and sJ3<>0 then print("10:ellipse inside hyperbola");
	   fi;
#I1=0:
elif vari(sF)=0 and sJ3<>0 and vari(sJ)=2 then print("3i: one inner tangent point");
	
elif vari(sJ)=0 and sG1<>0 and sJ3<>0 then print("5i: two points of intersection and an inner tangent point") ;  

elif vari(sJ)=0 and sJ3=0 and sJ5<0 and sJ1=0 then print("5ii: two points of intersection and an inner tangent point") ;
elif vari(sF)=2 and vari(sK)=0 and sK3<>0 then print("6: one outer tangent point"); 
elif vari(sK)=2 and sK3<>0 then print("8: two points of intersection and one outer tangent point");

elif sG1=0 and sJ3<0 and sJ5<0 then print("11: an intersection and an inner tangent point");  

fi;



end:
 
